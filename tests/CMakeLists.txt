find_package(Catch2 CONFIG REQUIRED)
find_package(Boost 1.70 REQUIRED)
find_package(fmt REQUIRED)
find_package(nlohmann_json CONFIG REQUIRED)

## Speed up the compile-time process
add_library(catch_main STATIC catch_main.cpp)
target_link_libraries(catch_main PUBLIC Catch2::Catch2 Catch2::Catch2WithMain)

include(CompilerWarnings)

function(test filename)
    set(target_name test.${filename})
    add_executable(${target_name})
    set_project_warnings(${target_name})

    target_sources(${target_name} PRIVATE ${filename}.cpp)

if (SEC21_ENABLE_COVERAGE)
    target_compile_options(${target_name} PRIVATE "--coverage")
    target_link_libraries(${target_name} PRIVATE gcov)
endif ()

    target_link_libraries(${target_name} PRIVATE Boost::boost)
    target_link_libraries(${target_name} PRIVATE catch_main)
    target_link_libraries(${target_name} PRIVATE sec21::sec21)
    target_link_libraries(${target_name} PRIVATE fmt::fmt-header-only)
    target_link_libraries(${target_name} PRIVATE nlohmann_json nlohmann_json::nlohmann_json)

    add_test(
            NAME ${target_name}
            COMMAND $<TARGET_FILE:${target_name}>)
endfunction()

## Unit tests via catch (alphabetical order)
test("all_of")
test("alphabet")
test("any_of")
test("arity")
test("bit_pattern")
test("blob")
test("concat")
test("database.column_type")
test("database.constraints")
test("database.query")
test("fixed_string")
test("for_each_adjacent")
test("for_each_chunk")
test("for_each_indexed")
test("limited_quantity")
test("literals.memory")
test("reflection.get_column")
test("scope_guard")
test("split_if")
test("strong_type")
test("transform")
test("transform_if")
test("to_array")
test("type_traits.contains")
test("type_traits.index_of")
test("type_traits.is_specialized")
test("type_traits.is_std_string")
test("type_traits.is_tuple")
test("units.dimension")
test("units.quantity")
test("units.ratio")
test("validate")
test("write_csv")
test("zip")
